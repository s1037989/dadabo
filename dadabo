use Mojolicious::Lite;

use Mojo::Util 'md5_sum';

use Storable;

helper sample => sub {
  my @dna = qw(a c g t);
  return join '', map { $dna[int(rand($#dna+1))] } 1..605;
};

helper compare => sub {
  my ($c, $a, $b) = @_;
  $_ = $a ^ $b;
  s/./ord $& ? "^" : " "/ge;
  return $_;
};

get '/' => sub {
  my $c = shift;
  $c->stash(id => substr(md5_sum(time.$$.rand), 0, 6));
} => 'index';

post '/:id' => sub {
  my $c = shift;
  store({map { $_ => $c->param($_) } qw/id name_a a name_b b/}, $c->app->home->child('id')->make_path->child($c->param('id')));
  $c->redirect_to(id => {id => $c->param('id')});
};

get '/sample' => sub {
  my $c = shift;
  my $a = $c->sample;
  my $b = $c->sample;
  $c->render('id', id => 'sample', name_a => 'A', a => $a, name_b => 'B', b => $b, d => $c->compare($a, $b));
};

get '/:id' => sub {
  my $c = shift;
  my $data = retrieve($c->app->home->child('id')->make_path->child($c->param('id')));
  $c->render(%$data, d => $c->compare($data->{a}, $data->{b}));
};

app->start;

__DATA__

@@ index.html.ep
<p><%= link_to Sample => 'sample' %></p>
%= form_for id => {id => $id} => (method => 'POST') => begin
%= hidden_field id => $id
<p>
%= label_for name_a => 'Name A'
%= text_field 'name_a'
</p>
<p>
%= label_for a => 'Seq A'
%= text_area 'a', cols => 80
</p>
<p>
%= label_for name_b => 'Name B'
%= text_field 'name_b'
</p>
<p>
%= label_for b => 'Seq B'
%= text_area 'b', cols => 80
</p>
<p>
%= label_for submit => 'Show differences'
%= submit_button 'submit'
</p>
% end

@@ id.html.ep
% use List::Util qw(max min);
<p><%= link_to New => 'index' %></p>
<p>ID: <%= link_to $id => 'id' => {id => $id } %></p>
<p>Name A: <%= $name_a %></p>
<p>Name B: <%= $name_b %></p>
<pre>
  % my $break = min(length($a), length($b), 80);
  % for ( my $i = 1; $i <= max(length($a), length($b)); $i+=$break ) {
  <%= substr($a, $i-1, $break) %>
  <%= substr($b, $i-1, $break) %>
  <%= substr($d, $i-1, $break) %>

  % }
</pre>

